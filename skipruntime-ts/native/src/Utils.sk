module SkipRuntime;

const kSessionDir: SKStore.DirName = SKStore.DirName::create(
  "/sk_prv/sessions/",
);
const kTokenDir: SKStore.DirName = SKStore.DirName::create("/sk_prv/tokens/");
const kGraphDir: SKStore.DirName = SKStore.DirName::create("/sk_prv/graph/");
const kResourceSessionDir: SKStore.DirName = SKStore.DirName::create(
  "/sk_prv/resources/sessions/",
);
const kResourceGraphDir: SKStore.DirName = SKStore.DirName::create(
  "/sk_prv/resources/graph/",
);
const kResourceCollectionsDir: SKStore.DirName = SKStore.DirName::create(
  "/sk_prv/resources/collections/",
);
const kResourceGarbageDir: SKStore.DirName = SKStore.DirName::create(
  "/sk_prv/resources/garbage/",
);

const kResourceInstance2SubIdDir: SKStore.DirName = SKStore.DirName::create(
  "/sk_prv/resources/instance2subid/",
);

class JSONID(json: SKJSON.CJSON) extends SKStore.Key {
  //
  fun toString(): String {
    this.json.prettyPrint()
  }
}

class JSONFile(json: SKJSON.CJSON) extends SKStore.Intable {
  fun +<T: Integral>(x: T): JSONFile {
    JSONFile(SKJSON.CJInt(this.toInt() + x))
  }

  fun -<T: Integral>(x: T): JSONFile {
    JSONFile(SKJSON.CJInt(this.toInt() - x))
  }

  fun toString(): String {
    this.json.prettyPrint()
  }

  fun toInt(): Int {
    this.json match {
    | SKJSON.CJInt(x) -> x
    | _ -> throw SKStore.Error("Non-number JSON")
    };
  }
}

const kJSON_CONVERTERS: ConverterPair<JSONID, JSONFile> = ConverterPair(
  JSONID::keyType,
  JSONFile::type,
);

const kNOTYPE_CONVERTERS: ConverterPair<SKStore.Key, SKStore.File> = ConverterPair(
  k ~> k,
  v ~> v,
);

fun accReducer<V1: SKStore.File, V2: SKStore.File>(
  reducer: Reducer<V1, V2>,
): SKStore.EReducer<V1, V2> {
  SKStore.EReducer{
    type => reducer.converter(),
    canReset => true,
    init => iter ~> {
      acc = reducer.initial;
      for (x in iter) {
        !acc = reducer.add(acc, x)
      };
      Array[acc]
    },
    update => (state, old, new) ~> {
      acc = state[0];
      for (x in old) {
        reducer.remove(acc, x) match {
        | Some(v) -> !acc = v
        | _ -> return None()
        }
      };
      for (x in new) {
        !acc = reducer.add(acc, x)
      };
      Some(Array[acc])
    },
  }
}

fun identityMap<K: SKStore.Key, V: SKStore.File>(
  _context: mutable SKStore.Context,
  writer: mutable SKStore.TWriter<K, V>,
  key: K,
  it: mutable SKStore.NonEmptyIterator<V>,
): void {
  writer.setArray(key, Array::createFromIterator(it.values())); // TODO: use it.toArray()
}

fun noFilter<K: SKStore.Key, V: SKStore.File>(
  _context: readonly SKStore.Context,
  _key: K,
  _value: V,
): Bool {
  true
}

fun countElementAsOne<V: SKStore.File>(_value: SKStore.File): Int {
  1
}

fun removeElementAsOne<V: SKStore.File>(
  _value: V,
  _toRemove: Int,
): SKStore.File {
  invariant_violation("removeElementAsOne called on singleton value");
}

fun subDirName(
  context: mutable SKStore.Context,
  name: String,
): SKStore.DirName {
  path = context.currentArrow() match {
  | Some(arrow) ->
    b64 = base64(arrow.key);
    `${arrow.childName}${b64}/${name}/`
  | None() -> `/sk/${name}/`
  };
  SKStore.DirName::create(path)
}

fun base64<T: Show>(toEncode: T): String {
  encoded = `b64_${Base64.encodeString(toEncode.toString(), true)}`;
  encoded.searchRight(ch ~> ch != '=') match {
  | Some(i) ->
    _ = i.next();
    encoded.getIter().substring(i)
  | None() -> ""
  }
}
fun toResourceId(resource: String, params: Parameters): String {
  `${resource}_${base64(params.toString())}`
}

fun toSuppliedResourceId(
  supplier: String,
  resource: String,
  params: Parameters,
): String {
  `${supplier}_${toResourceId(resource, params)}`;
}

fun collectionForNameJSON(name: String): Collection<JSONID, JSONFile> {
  Collection::forName(name, kJSON_CONVERTERS)
}

fun collectionForName(name: String): Collection<SKStore.Key, SKStore.File> {
  Collection::forName(name, kNOTYPE_CONVERTERS)
}

fun lazyForNameJSON(name: String): LazyCollection<JSONID, JSONFile> {
  LazyCollection::forName(name, kJSON_CONVERTERS)
}

fun lazyForName(name: String): LazyCollection<SKStore.Key, SKStore.File> {
  LazyCollection::forName(name, kNOTYPE_CONVERTERS)
}

fun collectionsByName(
  collections: Map<String, Collection<SKStore.Key, SKStore.File>>,
): SKJSON.CJObject {
  fields = mutable Vector[];
  collections.each((k, v) -> fields.push((k, SKJSON.CJString(v.getId()))));
  SKJSON.CJObject(SKJSON.CJFields::create(fields.toArray(), x -> x));
}

module end;
